/*
 * Copyright 2017 Vorlonsoft LLC
 *
 * Licensed under The MIT License (MIT)
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.vorlonsoft.android.rate.sample"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 7
        versionName "1.1.5"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            initWith debug
            applicationIdSuffix ""
            zipAlignEnabled true
            debuggable false
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
        debug {
            applicationIdSuffix ".debug"
            zipAlignEnabled false
            debuggable true
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            embedMicroApp false
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
            }
            matchingFallbacks = ['release']
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}.apk"
        }
    }
}
dependencies {
    implementation project(':library')
    implementation 'com.google.android.gms:play-services-base:11.8.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}